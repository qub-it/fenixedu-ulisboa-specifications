/**
 * This file was created by Quorum Born IT <http://www.qub-it.com/> and its 
 * copyright terms are bind to the legal agreement regulating the FenixEdu@ULisboa 
 * software development project between Quorum Born IT and Serviços Partilhados da
 * Universidade de Lisboa:
 *  - Copyright © 2015 Quorum Born IT (until any Go-Live phase)
 *  - Copyright © 2015 Universidade de Lisboa (after any Go-Live phase)
 *
 * Contributors: paulo.abrantes@qub-it.com
 * Contributors: nuno.pinheiro@qub-it.com
 *
 * 
 * This file is part of FenixEdu fenixedu-ulisboa-specifications.
 *
 * FenixEdu fenixedu-ulisboa-specifications is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Lesser General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * FenixEdu fenixedu-ulisboa-specifications is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public License
 * along with FenixEdu fenixedu-ulisboa-specifications.  If not, see <http://www.gnu.org/licenses/>.
 */

package org.fenixedu.ulisboa.specifications.domain;


enum org.fenixedu.academic.domain.GradeScale                                                                           as GradeScaleSpecifications;

enum org.fenixedu.ulisboa.specifications.domain.serviceRequests.UIComponentType as UIComponentType;
enum org.fenixedu.ulisboa.specifications.domain.evaluation.markSheet.CompetenceCourseMarkSheetStateEnum as CompetenceCourseMarkSheetStateEnum;

external class .org.fenixedu.academic.domain.degree.DegreeType;
external class .org.fenixedu.academic.domain.CompetenceCourse;
external class .org.fenixedu.academic.domain.EvaluationSeason;
external class .org.fenixedu.academic.domain.ExecutionSemester;
external class .org.fenixedu.academic.domain.Person;
external class .org.fenixedu.academic.domain.OccupationPeriod;
external class .org.fenixedu.academic.domain.curricularPeriod.CurricularPeriod;
external class .org.fenixedu.academic.domain.curricularRules.AnyCurricularCourseExceptions;
external class .org.fenixedu.academic.domain.student.StatuteType;
external class .org.fenixedu.academic.domain.student.Registration;
external class .org.fenixedu.academic.domain.SchoolClass;
external class .org.fenixedu.academic.domain.Shift;
external class .org.fenixedu.academic.domain.serviceRequests.AcademicServiceRequest;
external class .org.fenixedu.academic.domain.serviceRequests.ServiceRequestType;
external class .org.fenixedu.academic.domain.student.Registration;
external class .org.fenixedu.academic.domain.serviceRequests.documentRequests.DocumentPurposeTypeInstance;
external class .org.fenixedu.academic.domain.ExecutionYear;
external class .org.fenixedu.academic.domain.studentCurriculum.ExternalEnrolment;
external class .org.fenixedu.academic.domain.StudentCurricularPlan;
external class .org.fenixedu.bennu.core.domain.User;
external class .org.fenixedu.bennu.io.domain.GenericFile;

class UsernameSequenceGenerator {
	String prefix;
	protected Integer currentValue;
}

class idcards.CgdCard {
	String mifareCode;
	String cardNumber;
	LocalDate lastMifareModication;
	LocalDate issueDate;
	LocalDate validUntil;
	boolean temporary;
}

relation CgdCardPerson {
	idcards.CgdCard playsRole cgdCards {
		multiplicity *;
	}
	.org.fenixedu.academic.domain.Person playsRole person;
}

relation CgdCardRootDomainObject {
	idcards.CgdCard playsRole cgdCards {
		multiplicity *;
	}
	.org.fenixedu.bennu.core.domain.Bennu playsRole rootDomainObject;
}

class DisabilityType {
	String code (REQUIRED);
	LocalizedString description (REQUIRED);
	boolean other;
}

relation DisabilityTypeBennu {
	.org.fenixedu.bennu.core.domain.Bennu playsRole root {
        multiplicity 1..1;
    }
	DisabilityType playsRole disabilityTypes {
		multiplicity *;
	}
}

class UniversityDiscoveryMeansAnswer {
	String code (REQUIRED);
	LocalizedString description (REQUIRED);
	boolean other;
}

relation UniversityDiscoveryMeansAnswerBennu {
	.org.fenixedu.bennu.core.domain.Bennu playsRole root {
        multiplicity 1..1;
    }
	UniversityDiscoveryMeansAnswer playsRole universityDiscoveryMeansAnswers {
		multiplicity *;
	}
}

class UniversityChoiceMotivationAnswer {
	String code (REQUIRED);
	LocalizedString description (REQUIRED);
	boolean other;
}

relation UniversityChoiceMotivationAnswerBennu {
	.org.fenixedu.bennu.core.domain.Bennu playsRole root {
        multiplicity 1..1;
    }
	UniversityChoiceMotivationAnswer playsRole universityChoiceMotivationAnswers {
		multiplicity *;
	}
}

class SalarySpan {
	String code (REQUIRED);
	LocalizedString description (REQUIRED);
	boolean other;
}

relation SalarySpanBennu {
	.org.fenixedu.bennu.core.domain.Bennu playsRole root {
        multiplicity 1..1;
    }
	SalarySpan playsRole salarySpans {
		multiplicity *;
	}
}

class ResidenceType {
	String code (REQUIRED);
	LocalizedString description (REQUIRED);
	boolean other;
}

relation ResidenceTypeBennu {
	.org.fenixedu.bennu.core.domain.Bennu playsRole root {
        multiplicity 1..1;
    }
	ResidenceType playsRole residenceTypes {
		multiplicity *;
	}
}

class ProfessionTimeType {
	String code (REQUIRED);
	LocalizedString description (REQUIRED);
	boolean other;
}

relation ProfessionTimeTypeBennu {
	.org.fenixedu.bennu.core.domain.Bennu playsRole root {
        multiplicity 1..1;
    }
	ProfessionTimeType playsRole professionTimeTypes {
		multiplicity *;
	}
}

class PersonUlisboaSpecifications {
	LocalDate vaccinationValidity;
	boolean hasDisabilities;
	String otherDisabilityType;
	//Boolean because this slot must support null (= not filled in) 
	Boolean needsDisabilitySupport;
	String otherUniversityDiscoveryMeans;
	String otherUniversityChoiceMotivation;
	String otherDislocatedResidenceType;
    String firstOptionDegreeDesignation;
    boolean authorizeSharingDataWithCGD;
    String dgesTempIdCode;
    String oldInformation;
}

relation PersonUlPerson {
	PersonUlisboaSpecifications playsRole personUlisboaSpecifications;
	.org.fenixedu.academic.domain.Person playsRole person;
}

relation PersonUlSecondNationality {
	PersonUlisboaSpecifications playsRole personUlisboaSpecifications {
		multiplicity *;
	}
    .org.fenixedu.academic.domain.Country playsRole secondNationality;
}

relation PersonUlFirstOptionInstitution {
	PersonUlisboaSpecifications playsRole personUlisboaSpecifications {
		multiplicity *;
	}
    .org.fenixedu.academic.domain.organizationalStructure.Unit playsRole firstOptionInstitution;
}

relation PersonUlProfessionTimeType {
	PersonUlisboaSpecifications playsRole personUlisboaSpecifications {
		multiplicity *;
	}
	ProfessionTimeType playsRole professionTimeType;
}

relation PersonUlSalarySpan {
	PersonUlisboaSpecifications playsRole personUlisboaSpecifications {
		multiplicity *;
	}
	SalarySpan playsRole householdSalarySpan;
}

relation PersonUlResidenceType {
	PersonUlisboaSpecifications playsRole personUlisboaSpecifications {
		multiplicity *;
	}
	ResidenceType playsRole dislocatedResidenceType;
}

relation PersonUlDisabilityType {
	PersonUlisboaSpecifications playsRole personUlisboaSpecifications {
		multiplicity *;
	}
	DisabilityType playsRole disabilityType;
}


relation PersonUlUniversityDiscoveryMeansAnswer {
	PersonUlisboaSpecifications playsRole personUlisboaSpecifications {
		multiplicity *;
	}
	UniversityDiscoveryMeansAnswer playsRole universityDiscoveryMeansAnswers {
		multiplicity *;
	}
}

relation PersonUlUniversityChoiceMotivationAnswer {
	PersonUlisboaSpecifications playsRole personUlisboaSpecifications {
		multiplicity *;
	}
	UniversityChoiceMotivationAnswer playsRole universityChoiceMotivationAnswers {
		multiplicity *;
	}
}

relation PrecedentDegreeInformationDistrict{
	.org.fenixedu.academic.domain.student.PrecedentDegreeInformation playsRole precedentDegreeInformation;
	.org.fenixedu.academic.domain.District playsRole district;
}

relation PrecedentDegreeInformationDistrictSubdivision{
	.org.fenixedu.academic.domain.student.PrecedentDegreeInformation playsRole precedentDegreeInformation;
	.org.fenixedu.academic.domain.DistrictSubdivision playsRole districtSubdivision;
}

// ==================== ULisboa Specifications Root ====================

class ULisboaSpecificationsRoot {
}

relation ULisboaSpecificationsRootBennu {
	ULisboaSpecificationsRoot playsRole uLisboaSpecificationsRoot;
	.org.fenixedu.bennu.core.domain.Bennu playsRole rootDomainObject;
}

relation ULisboaSpecificationsRootRegistrationProtocol {
	ULisboaSpecificationsRoot playsRole uLisboaSpecificationsRoot;
	.org.fenixedu.academic.domain.student.RegistrationProtocol playsRole defaultRegistrationProtocol;
}

class ContingentToIngression {
	String contingent;
}

relation ContingentToIngressionIngressionType {
	.org.fenixedu.academic.domain.candidacy.IngressionType playsRole ingressionType;
	ContingentToIngression playsRole contingentToIngressions {
		multiplicity *;
	}
}

relation ULisboaSpecificationsRootContingentToIngressions {
	ULisboaSpecificationsRoot playsRole uLisboaSpecificationsRoot;
	ContingentToIngression playsRole contingentToIngressions {
		multiplicity *;
	}
}

relation ULisboaSpecificationsRootUsernameSequenceGenerator {
	UsernameSequenceGenerator playsRole usernameSequenceGenerator;
	ULisboaSpecificationsRoot playsRole uLisboaSpecificationsRoot;
}

// ==================== DGES Student Import ====================

class student.access.importation.DgesStudentImportationProcess extends .org.fenixedu.academic.domain.QueueJobWithFile {
	EntryPhase entryPhase;
}

class student.access.importation.DgesStudentImportationFile extends .org.fenixedu.academic.domain.File {
}

relation ImportCandidaciesFromDgesProcessDgesImportationStudentFile {
	student.access.importation.DgesStudentImportationProcess playsRole dgesStudentImportationProcess;
	student.access.importation.DgesStudentImportationFile playsRole dgesStudentImportationFile;
}

relation DgesStudentImportationProcessExecutionYear {
	.org.fenixedu.academic.domain.ExecutionYear playsRole executionYear;
	student.access.importation.DgesStudentImportationProcess playsRole dgesStudentImportationProcess {
		multiplicity *;
	}
}

relation DgesStudentImportationProcessSpace {
	.org.fenixedu.spaces.domain.Space playsRole space;
	student.access.importation.DgesStudentImportationProcess playsRole dgesStudentImportationProcess {
		multiplicity*;
	}
}

relation DgesStudentImportationProcessStudentCandidacy {
	student.access.importation.DgesStudentImportationProcess playsRole dgesStudentImportationProcess;
	.org.fenixedu.academic.domain.candidacy.StudentCandidacy playsRole studentCandidacy {
		multiplicity *;
	}
}

class candidacy.FirstTimeCandidacy extends .org.fenixedu.academic.domain.candidacy.StudentCandidacy {
}


// ==================== First Year School Configuration ====================

class FirstYearRegistrationConfiguration{
	boolean requiresVaccination;
	boolean requiresCoursesEnrolment;
	boolean requiresClassesEnrolment;
	boolean requiresShiftsEnrolment;
}

relation DegreeFirstYearRegistrationConfiguration{
	.org.fenixedu.academic.domain.Degree playsRole degree{
		multiplicity 1;
	}
	FirstYearRegistrationConfiguration playsRole firstYearRegistrationConfiguration;
}

class FirstYearRegistrationGlobalConfiguration {
	LocalizedString introductionText;
}

class CgdMod43Template extends .org.fenixedu.bennu.io.domain.GenericFile {
}

relation BennuFirstYearRegistrationGlobalConfiguration {
	.org.fenixedu.bennu.core.domain.Bennu playsRole bennu;
	FirstYearRegistrationGlobalConfiguration playsRole firstYearRegistrationGlobalConfigurations {
		multiplicity *;
	}
}

relation DegreeConfigurationGlobalConfiguration {
	FirstYearRegistrationGlobalConfiguration playsRole globalConfiguration {
		multiplicity 1;
	}
	FirstYearRegistrationConfiguration playsRole firstYearRegistrationConfigurations {
		multiplicity *;
	}
}

relation GlobalConfigurationTemplate {
	FirstYearRegistrationGlobalConfiguration playsRole globalConfiguration {
		multiplicity 1;
	}
	CgdMod43Template playsRole mod43Template;
}	

//Class to bypass PortalLayoutInjector limitations. Check the java file for more information
class ULisboaPortalConfiguration{
}

relation ULisboaPortalConfigurationPortalConfiguration{
	.org.fenixedu.bennu.portal.domain.PortalConfiguration playsRole portal{
		multiplicity 1;
	}
	ULisboaPortalConfiguration playsRole ulisboaPortal{
		multiplicity 1;
	}
}


// ==================== BEGIN: MaximumNumberOfCreditsForEnrolmentPeriodEnforcer ====================

class MaximumNumberOfCreditsForEnrolmentPeriodEnforcer {
	Double maxCredits;
	Double maxCreditsPartialTime;
}

relation ULisboaSpecificationsRootMaximumNumberOfCreditsForEnrolmentPeriodEnforcer {
	ULisboaSpecificationsRoot playsRole uLisboaSpecificationsRoot;
	MaximumNumberOfCreditsForEnrolmentPeriodEnforcer playsRole maximumNumberOfCreditsForEnrolmentPeriodEnforcer {
		multiplicity 0..1;
	}
}

// ==================== END: MaximumNumberOfCreditsForEnrolmentPeriodEnforcer ====================

// ==================== BEGIN: CurricularPeriodConfiguration ====================

class curricularPeriod.CurricularPeriodConfiguration {
}

relation CurricularPeriodConfigurationRoot {
	ULisboaSpecificationsRoot playsRole uLisboaSpecificationsRoot {
		multiplicity 1..1;
	}
	curricularPeriod.CurricularPeriodConfiguration playsRole curricularPeriodConfiguration {
		multiplicity *;
	}
}

relation CurricularPeriodConfigurations {
	.org.fenixedu.academic.domain.curricularPeriod.CurricularPeriod playsRole curricularPeriod {
		multiplicity 1..1;
	}
	curricularPeriod.CurricularPeriodConfiguration playsRole configuration {
		multiplicity 0..1;
	}
}

class curricularPeriod.rule.CurricularPeriodRule {
	BigDecimal credits;
	boolean hideMessagePrefix;
}

	class curricularPeriod.rule.RuleEnrolment extends curricularPeriod.rule.CurricularPeriodRule {
		Integer semester;
	}
	
		class curricularPeriod.rule.enrolment.CreditsInEnrolmentPeriod extends curricularPeriod.rule.RuleEnrolment {
			boolean includeEnrolments;
		}
		
		class curricularPeriod.rule.enrolment.CreditsInCurricularPeriod extends curricularPeriod.rule.RuleEnrolment {
			Integer yearMin;
			Integer yearMax;
		}
		
		class curricularPeriod.rule.enrolment.CreditsNotEnroled extends curricularPeriod.rule.RuleEnrolment {
			Integer yearMin;
		}
		
		class curricularPeriod.rule.enrolment.CreditsEnroledAsFirstTime extends curricularPeriod.rule.RuleEnrolment {
		
		}

	class curricularPeriod.rule.RuleTransition extends curricularPeriod.rule.CurricularPeriodRule {
		Integer yearMin;
		Integer yearMax;
	}

		class curricularPeriod.rule.transition.FlunkedCredits extends curricularPeriod.rule.RuleTransition {
		}
		
		class curricularPeriod.rule.transition.ApprovedCredits extends curricularPeriod.rule.RuleTransition {
		}
		
		class curricularPeriod.rule.transition.FlunkedCurricularCourses extends curricularPeriod.rule.RuleTransition {
			String codesCSV;
		}
		
		class curricularPeriod.rule.transition.StudentStatuteExecutiveRule extends curricularPeriod.rule.RuleTransition {
		}
		
relation CurricularPeriodConfigurationRuleEnrolment {
	curricularPeriod.CurricularPeriodConfiguration playsRole configurationEnrolment {
		multiplicity 1..1;
	}
	curricularPeriod.rule.RuleEnrolment playsRole ruleEnrolment {
		multiplicity *;
	}
}

relation CurricularPeriodConfigurationRuleTransition {
	curricularPeriod.CurricularPeriodConfiguration playsRole configurationTransition {
		multiplicity 1..1;
	}
	curricularPeriod.rule.RuleTransition playsRole ruleTransition {
		multiplicity *;
	}
}

relation StatuteTypeRuleTransition {
	.org.fenixedu.academic.domain.student.StatuteType playsRole statuteType {
		multiplicity 1..1;
	}
	curricularPeriod.rule.transition.StudentStatuteExecutiveRule playsRole ruleTransition {
		multiplicity *;
	}
}

// ==================== END: CurricularPeriodConfiguration ====================

// ==================== BEGIN: CurricularRule Executor Logics ====================

class curricularRules.AnyCurricularCourseExceptionsConfiguration {
}

relation AnyCurricularCourseExceptionsConfigurationRoot {
	ULisboaSpecificationsRoot playsRole uLisboaSpecificationsRoot {
		multiplicity 1..1;
	}
	curricularRules.AnyCurricularCourseExceptionsConfiguration playsRole anyCurricularCourseExceptionsConfiguration {
		multiplicity 0..1;
	}
}

relation AnyCurricularCourseExceptionsCompetenceCourse {
	curricularRules.AnyCurricularCourseExceptionsConfiguration playsRole anyCurricularCourseExceptionsConfiguration {
		multiplicity *;
	}
	.org.fenixedu.academic.domain.CompetenceCourse playsRole competenceCourses {
		multiplicity *;
	}
}

// ==================== END: CurricularRule Executor Logics ====================

// ==================== BEGIN: Evaluations ====================

class evaluation.season.EvaluationSeasonPeriod {
}



class evaluation.config.MarkSheetSettings {
	boolean allowTeacherToChooseCertifier;
	boolean requiresExactlyOneShift;
	boolean limitCertifierToResponsibleTeacher;
}

relation MarkSheetSettingsRoot {
   
    ULisboaSpecificationsRoot playsRole uLisboaSpecificationsRoot {
    	multiplicity 1..1;
    }
    
    evaluation.config.MarkSheetSettings playsRole markSheetSettings {
        multiplicity 0..1;
    }
}

class evaluation.config.CompetenceCourseMarkSheetTemplateFile extends .org.fenixedu.bennu.io.domain.GenericFile {

}

relation CompetenceCourseMarkSheetTemplateFileMarkSheetSettings {

	evaluation.config.MarkSheetSettings playsRole markSheetSettings {
    	multiplicity 1..1;
    }
    
    evaluation.config.CompetenceCourseMarkSheetTemplateFile playsRole templateFile {
        multiplicity 0..1;
    }
}


relation EvaluationSeasonPeriodRelation {
    protected evaluation.season.EvaluationSeasonPeriod playsRole evaluationSeasonPeriod {
        multiplicity *;
    }

    .org.fenixedu.academic.domain.EvaluationSeason playsRole season {
        multiplicity 1;
    }
}

relation EvaluationSeasonOccupationPeriod {
    protected evaluation.season.EvaluationSeasonPeriod playsRole evaluationSeasonPeriod {
        multiplicity *;
    }

    protected .org.fenixedu.academic.domain.OccupationPeriod playsRole occupationPeriod {
        multiplicity 1;
    }
}

relation EvaluationSeasonPeriodExecutionSemester {
    evaluation.season.EvaluationSeasonPeriod playsRole evaluationSeasonPeriod {
        multiplicity *;
    }

    .org.fenixedu.academic.domain.ExecutionSemester playsRole executionSemester {
        multiplicity 1;
    }
}

class evaluation.season.EvaluationSeasonInformation {
	Integer seasonOrder;
	boolean active;
}

relation EvaluationSeasonInformationRelation {
    evaluation.season.EvaluationSeasonInformation playsRole information {
        multiplicity 1..1;
    }

    .org.fenixedu.academic.domain.EvaluationSeason playsRole season {
        multiplicity 1..1;
    }
}

class evaluation.season.rule.EvaluationSeasonRule {
}

	class evaluation.season.rule.PreviousSeasonApproval extends evaluation.season.rule.EvaluationSeasonRule {
	}

	class evaluation.season.rule.PreviousSeasonMinimumGrade extends evaluation.season.rule.EvaluationSeasonRule {
		Grade minimum;
	}

	class evaluation.season.rule.PreviousSeasonBlockingGrade extends evaluation.season.rule.EvaluationSeasonRule {
		Grade blocking;
	}

	class evaluation.season.rule.GradeScaleValidator extends evaluation.season.rule.EvaluationSeasonRule {
		GradeScale gradeScale;
		String gradeValues;
		LocalizedString ruleDescription;
	}

relation EvaluationSeasonRuleRelation {
    evaluation.season.rule.EvaluationSeasonRule playsRole rules {
        multiplicity *;
    }

    .org.fenixedu.academic.domain.EvaluationSeason playsRole season {
        multiplicity 1;
    }
}

relation GradeScaleValidatorDegreeTypes {
    evaluation.season.rule.GradeScaleValidator playsRole gradeScaleValidator {
        multiplicity *;
    }

    .org.fenixedu.academic.domain.degree.DegreeType playsRole degreeType {
        multiplicity 1..*;
    }
}

class evaluation.markSheet.CompetenceCourseMarkSheet {
    LocalDate evaluationDate;
    boolean printed;
    GradeScaleSpecifications gradeScale;
}

class evaluation.markSheet.CompetenceCourseMarkSheetStateChange {
    CompetenceCourseMarkSheetStateEnum state;
    DateTime date;
    String reason;
    boolean byTeacher;
}

class evaluation.markSheet.CompetenceCourseMarkSheetSnapshot {
	String competenceCourseCode;
	LocalizedString competenceCourseName;
	String executionSemester;
	LocalizedString evaluationSeason;
	String certifier;
	LocalDate evaluationDate;
	String checkSum;
	String shifts;
}

class evaluation.markSheet.CompetenceCourseMarkSheetSnapshotEntry {
    Integer studentNumber;
    String studentName;
    Grade grade;
    String degreeCode;
    LocalizedString degreeName;
    String shifts;
}

relation CompetenceCourseMarkSheetStateChangeSnapshot {
	evaluation.markSheet.CompetenceCourseMarkSheetSnapshot playsRole snapshot {
		multiplicity 0..1;
	}
	
	evaluation.markSheet.CompetenceCourseMarkSheetStateChange playsRole stateChange {
		multiplicity 1..1;
	}
}

relation CompetenceCourseMarkSheetSnapshotEntryRelation {
	protected evaluation.markSheet.CompetenceCourseMarkSheetSnapshot playsRole snapshot {
		multiplicity 1..1;
	}
	
	evaluation.markSheet.CompetenceCourseMarkSheetSnapshotEntry playsRole entry {
		multiplicity 1..*;
	}
}

relation CompetenceCourseMarkSheetStateChangeRelation {
	protected evaluation.markSheet.CompetenceCourseMarkSheet playsRole competenceCourseMarkSheet {
		multiplicity 1..1;
	}
	
	protected evaluation.markSheet.CompetenceCourseMarkSheetStateChange playsRole stateChange {
		multiplicity 1..*;
	}
}

relation CompetenceCourseMarkSheetStateChangeResponsible {
    protected evaluation.markSheet.CompetenceCourseMarkSheetStateChange playsRole competenceCourseMarkSheetChange {
        multiplicity *;
    }
    
    .org.fenixedu.academic.domain.Person playsRole responsible {
		multiplicity 1..1;
	}
}

relation CompetenceCourseMarkSheetRelation {
    evaluation.markSheet.CompetenceCourseMarkSheet playsRole competenceCourseMarkSheet {
        multiplicity *;
    }
    
    .org.fenixedu.academic.domain.CompetenceCourse playsRole competenceCourse {
		multiplicity 1..1;
	}
}

relation CompetenceCourseMarkSheetExecutionCourse {
    protected evaluation.markSheet.CompetenceCourseMarkSheet playsRole competenceCourseMarkSheet {
        multiplicity *;
    }
    
    .org.fenixedu.academic.domain.ExecutionCourse playsRole executionCourse {
		multiplicity 0..1;
	}
}

relation CompetenceCourseMarkSheetShift {
    protected evaluation.markSheet.CompetenceCourseMarkSheet playsRole competenceCourseMarkSheet {
        multiplicity *;
    }
    
    .org.fenixedu.academic.domain.Shift playsRole shift {
		multiplicity *;
	}
}

relation CompetenceCourseMarkSheetEnrolmentEvaluation {
    evaluation.markSheet.CompetenceCourseMarkSheet playsRole competenceCourseMarkSheet {
		multiplicity 0..1;
	}
    
    .org.fenixedu.academic.domain.EnrolmentEvaluation playsRole enrolmentEvaluation {
        multiplicity *;
    }
}

relation CompetenceCourseMarkSheetEvaluationSeason {
    protected evaluation.markSheet.CompetenceCourseMarkSheet playsRole competenceCourseMarkSheet {
        multiplicity *;
    }
    
    .org.fenixedu.academic.domain.EvaluationSeason playsRole evaluationSeason {
        multiplicity 1..1;
    }
}

relation CompetenceCourseMarkSheetExecutionSemester {
    .org.fenixedu.academic.domain.ExecutionSemester playsRole executionSemester {
        multiplicity 1..1;
    }
    
    evaluation.markSheet.CompetenceCourseMarkSheet playsRole competenceCourseMarkSheet {
        multiplicity *;
    }
}

relation CompetenceCourseMarkSheetCertifier {
    protected evaluation.markSheet.CompetenceCourseMarkSheet playsRole competenceCourseMarkSheet {
        multiplicity *;
    }

    .org.fenixedu.academic.domain.Person playsRole certifier {
		multiplicity 1..1;
	}
}

// ==================== END: Evaluations ====================


// ==================== Parishes as domain objects
class Parish{
	String code (REQUIRED);
    String name (REQUIRED);
}

relation ParsishDistrictSubdivision{
	.org.fenixedu.academic.domain.DistrictSubdivision playsRole districtSubdivision{
		multiplicity 1;
	}
	
	Parish playsRole parish{
		multiplicity *;
	}
}

relation ParsishesRoot {
	Parish playsRole parishes {
		multiplicity *;
	}
	.org.fenixedu.bennu.core.domain.Bennu playsRole root;
}


// ==================== END: Parishes as domain objects ====================


// ======================= = HelpdeskConfigurations = =========================

class helpdesk.HelpdeskConfigurations {
}

class helpdesk.HelpdeskRecipient {
	String email;
}

relation BennuHelpdeskConfigurations {
	helpdesk.HelpdeskConfigurations playsRole helpdeskConfiguration;
	.org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
		multiplicity 1..1;
	}
}

relation HelpdeskRecipients {
	helpdesk.HelpdeskConfigurations playsRole recipientConfiguration;
	helpdesk.HelpdeskRecipient playsRole recipients {
		multiplicity *;
	}
}

relation HelpdeskCCs {
	helpdesk.HelpdeskConfigurations playsRole cCConfiguration;
	helpdesk.HelpdeskRecipient playsRole cCs {
		multiplicity *;
	}
}

relation HelpdeskBCCs {
	helpdesk.HelpdeskConfigurations playsRole bCCConfiguration;
	helpdesk.HelpdeskRecipient playsRole bCCs {
		multiplicity *;
	}
}

// ======================= = ====================== = =========================

// ======================= = Access control specific for 2nd cycle candidates = =========================
class SpecialIngressionsAndOtherCyclesPersistentGroup extends .org.fenixedu.bennu.core.domain.groups.GroupConstant{
}
class SecondCycleFirstYearPersistentGroup extends .org.fenixedu.bennu.core.domain.groups.GroupConstant{
}

// ======================= = Enrolment in Scool Classes begin = =========================

relation RegistrationSchoolClass {
    .org.fenixedu.academic.domain.SchoolClass playsRole schoolClasses {
        multiplicity *;
    }
    .org.fenixedu.academic.domain.student.Registration playsRole registrations {
        multiplicity *;
    }
}

// ======================= = Enrolment in Scool Classes end = =========================


// ======================= = ULisboaServiceRequest = =========================

class serviceRequests.ULisboaServiceRequest extends .org.fenixedu.academic.domain.serviceRequests.AcademicServiceRequest {
    boolean isValid;
    boolean requestedOnline;
}

//======================= = ULisboaServiceRequestGeneratedDocument = =========================
class serviceRequests.ULisboaServiceRequestGeneratedDocument extends .org.fenixedu.bennu.io.domain.GenericFile;

relation ServiceRequestGeneratedDocumentOperator {
    .org.fenixedu.academic.domain.Person playsRole operator;
    serviceRequests.ULisboaServiceRequestGeneratedDocument playsRole generatedDocuments {
        multiplicity *;
    }
}

relation ServiceRequestGeneratedDocumentRequester {
    .org.fenixedu.academic.domain.Person playsRole requester;
    serviceRequests.ULisboaServiceRequestGeneratedDocument playsRole requestedDocuments {
        multiplicity *;
    }
}

relation ULisboaServiceRequestGeneratedDocuments {
    serviceRequests.ULisboaServiceRequest playsRole serviceRequest;
    serviceRequests.ULisboaServiceRequestGeneratedDocument playsRole generatedDocuments {
        multiplicity *;
    }
}

//======================= = ULisboaServiceRequestValidator = =========================
class serviceRequests.processors.ULisboaServiceRequestProcessor {
    LocalizedString name;
}

class serviceRequests.processors.ValidateProgramConclusionProcessor extends serviceRequests.processors.ULisboaServiceRequestProcessor {
    
}

class serviceRequests.processors.StateLoggerProcessor extends serviceRequests.processors.ULisboaServiceRequestProcessor {
    
}

class serviceRequests.processors.FillStandAlonePropertyProcessor extends serviceRequests.processors.ULisboaServiceRequestProcessor {

}

class serviceRequests.processors.FillEnrolmentsByYearPropertyProcessor extends serviceRequests.processors.ULisboaServiceRequestProcessor {

}

class serviceRequests.processors.FillStandaloneEnrolmentsByYearPropertyProcessor extends serviceRequests.processors.ULisboaServiceRequestProcessor {

}

class serviceRequests.processors.FillExtracurricularEnrolmentsByYearPropertyProcessor extends serviceRequests.processors.ULisboaServiceRequestProcessor {

}

class serviceRequests.processors.FillApprovedEnrolmentsPropertyProcessor extends serviceRequests.processors.ULisboaServiceRequestProcessor {

}

class serviceRequests.processors.FillAllPlansApprovementsPropertyProcessor extends serviceRequests.processors.ULisboaServiceRequestProcessor {

}

class serviceRequests.processors.FillCurriculumPropertyProcessor extends serviceRequests.processors.ULisboaServiceRequestProcessor {

}

class serviceRequests.processors.AutomaticOnlineRequestProcessor extends serviceRequests.processors.ULisboaServiceRequestProcessor {

}

relation ULisboaServiceRequestProcessorBennu {
    protected .org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
        multiplicity 1..1;
    }

    public serviceRequests.processors.ULisboaServiceRequestProcessor playsRole uLisboaServiceRequestProcessors {
        multiplicity *;
    }
}

relation ULisboaServiceRequestProcessorServiceRequestType {
    public .org.fenixedu.academic.domain.serviceRequests.ServiceRequestType playsRole serviceRequestTypes {
        multiplicity *;
    }

    public serviceRequests.processors.ULisboaServiceRequestProcessor playsRole uLisboaServiceRequestProcessors {
        multiplicity *;
    }
}

//======================= = Service Request Slot= =========================
class serviceRequests.ServiceRequestSlot {
    String code;
    UIComponentType uiComponentType;
    LocalizedString label;
    boolean changeable;
}

relation ServiceRequestSlotBennu {
    protected .org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
        multiplicity 1..1;
    }

    public serviceRequests.ServiceRequestSlot playsRole serviceRequestSlots {
        multiplicity *;
    }
}

//======================= = Service Request Slot Entry = =========================
class serviceRequests.ServiceRequestSlotEntry {
    Boolean required;
    int orderNumber;
}

relation ServiceRequestSlotEntryBennu {
    protected .org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
        multiplicity 1..1;
    }

    public serviceRequests.ServiceRequestSlotEntry playsRole serviceRequestSlotEntries {
        multiplicity *;
    }
}

relation ServiceRequestSlotEntryServiceRequestSlot {
    public serviceRequests.ServiceRequestSlot playsRole serviceRequestSlot {
        multiplicity 1..1;
    }

    public serviceRequests.ServiceRequestSlotEntry playsRole serviceRequestSlotEntries {
        multiplicity *;
    }
}
    
relation ServiceRequestSlotEntryServiceRequestType {    
    public .org.fenixedu.academic.domain.serviceRequests.ServiceRequestType playsRole serviceRequestType {
        multiplicity 1..1;
    }

    public serviceRequests.ServiceRequestSlotEntry playsRole serviceRequestSlotEntries {
        multiplicity *;
    }
}

//======================= = Service Request Property = =========================
class serviceRequests.ServiceRequestProperty {
    Locale locale;
    String string;
    LocalizedString localizedString;
    Boolean booleanValue;
    CycleType cycleType;
    Integer integer;
    DateTime dateTime;
}

relation ULisboaServiceRequestRegistration {
	public serviceRequests.ULisboaServiceRequest playsRole uLisboaServiceRequests {
		multiplicity *;
	}
	public .org.fenixedu.academic.domain.student.Registration playsRole registration;
}

relation ServiceRequestPropertyBennu {
    protected .org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
        multiplicity 1..1;
    }

    public serviceRequests.ServiceRequestProperty playsRole serviceRequestProperties {
        multiplicity *;
    }
}

relation ServiceRequestSlotServiceRequestProperty {
    public serviceRequests.ServiceRequestSlot playsRole serviceRequestSlot {
        multiplicity 1..1;
    }

    public serviceRequests.ServiceRequestProperty playsRole serviceRequestProperties {
        multiplicity *;
    }
}

relation ULisboaServiceRequestServiceRequestProperty {
    public serviceRequests.ULisboaServiceRequest playsRole uLisboaServiceRequest {
        multiplicity 0..1;
    }

    public serviceRequests.ServiceRequestProperty playsRole serviceRequestProperties {
        multiplicity *;
    }
}

//slots
relation DocumentPurposeTypeInstanceServiceRequestProperty {
    public .org.fenixedu.academic.domain.serviceRequests.documentRequests.DocumentPurposeTypeInstance playsRole documentPurposeTypeInstance {
        multiplicity 0..1;
    }

    public serviceRequests.ServiceRequestProperty playsRole serviceRequestProperties {
        multiplicity *;
    }
}

relation ProgramConclusionServiceRequestProperty {
    public .org.fenixedu.academic.domain.degreeStructure.ProgramConclusion playsRole programConclusion {
        multiplicity 0..1;
    }

    public serviceRequests.ServiceRequestProperty playsRole serviceRequestProperties {
        multiplicity *;
    }
}

relation ExecutionYearServiceRequestProperty {
    public .org.fenixedu.academic.domain.ExecutionYear playsRole executionYear {
        multiplicity 0..1;
    }

    public serviceRequests.ServiceRequestProperty playsRole serviceRequestProperties {
        multiplicity *;
    }
}

relation StudentCurricularPlanServiceRequestProperty {
    public .org.fenixedu.academic.domain.StudentCurricularPlan playsRole studentCurricularPlan {
        multiplicity 0..1;
    }

    public serviceRequests.ServiceRequestProperty playsRole serviceRequestProperties {
        multiplicity *;
    }
}

// ====================== = Service Request Restriction = ========================

class serviceRequests.ServiceRequestRestriction {
}

relation ServiceRequestTypeServiceRequestRestriction {
	.org.fenixedu.academic.domain.serviceRequests.ServiceRequestType playsRole serviceRequestType;
	
	serviceRequests.ServiceRequestRestriction playsRole serviceRequestRestrictions {
		multiplicity *;
	}	
}

relation ServiceRequestRestrictionDegreeType {
	.org.fenixedu.academic.domain.degree.DegreeType playsRole degreeType;

    serviceRequests.ServiceRequestRestriction playsRole serviceRequestRestrictions {
        multiplicity *;
    }
}

relation ServiceRequestRestrictionDegree {
	.org.fenixedu.academic.domain.Degree playsRole degree;

    serviceRequests.ServiceRequestRestriction playsRole serviceRequestRestrictions {
        multiplicity *;
    }
}

relation ServiceRequestRestrictionProgramConclusion {
	.org.fenixedu.academic.domain.degreeStructure.ProgramConclusion playsRole programConclusion;

    serviceRequests.ServiceRequestRestriction playsRole serviceRequestRestrictions {
        multiplicity *;
    }
}

/*
 * ***************************************************
 *    ICurriculum Entries
 * ***************************************************
 * 
 * ICurriculum Entries are stored in two variables, ExternalEnrolment and CurriculumLine, 
 * in order to create specific deletion listeners.
 */
relation ExternalEnrolmentServiceRequestProperty {
    protected .org.fenixedu.academic.domain.studentCurriculum.ExternalEnrolment playsRole externalEnrolments {
        multiplicity *;
    }

    public serviceRequests.ServiceRequestProperty playsRole serviceRequestProperties {
        multiplicity *;
    }
}

relation CurriculumLineServiceRequestProperty {
    protected .org.fenixedu.academic.domain.studentCurriculum.CurriculumLine playsRole curriculumLines {
        multiplicity *;
    }

    public serviceRequests.ServiceRequestProperty playsRole serviceRequestProperties {
        multiplicity *;
    }
}
/*
 * ***************************************************
 *    End ICurriculum Entries
 * ***************************************************
 */

//======================= = END all classes related to ULisboa Service Request = =========================


// ======================= Registration Observations =========================
class RegistrationObservations{
	String value;
}

relation RegistrationObservationRegistration{
	protected .org.fenixedu.academic.domain.student.Registration playsRole registration {
        multiplicity 1..1;
    }
    
    RegistrationObservations playsRole registrationObservations{
    	multiplicity *;
    }
}
// ======================= Registration Observations end =========================


/*
* Temporary Files
*/
class file.ULisboaSpecificationsTemporaryFile extends .org.fenixedu.bennu.io.domain.GenericFile {
        
}

relation ULisboaSpecificationsTemporaryFileRoot {
   
    ULisboaSpecificationsRoot playsRole uLisboaSpecificationsRoot {
    	multiplicity 1..1;
    }
    
    file.ULisboaSpecificationsTemporaryFile playsRole temporaryFiles {
        multiplicity *;
    }
}

relation ULisboaSpecificationsTemporaryFileUser {

	.org.fenixedu.bennu.core.domain.User playsRole uLisboaTemporaryFileOwner {
		multiplicity 1..1;
	}

	file.ULisboaSpecificationsTemporaryFile playsRole uLisboaTemporaryFiles {
        multiplicity *;
    }
}
