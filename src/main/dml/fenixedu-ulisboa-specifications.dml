/**
 * This file was created by Quorum Born IT <http://www.qub-it.com/> and its 
 * copyright terms are bind to the legal agreement regulating the FenixEdu@ULisboa 
 * software development project between Quorum Born IT and Serviços Partilhados da
 * Universidade de Lisboa:
 *  - Copyright © 2015 Quorum Born IT (until any Go-Live phase)
 *  - Copyright © 2015 Universidade de Lisboa (after any Go-Live phase)
 *
 * Contributors: paulo.abrantes@qub-it.com
 * Contributors: nuno.pinheiro@qub-it.com
 *
 * 
 * This file is part of FenixEdu fenixedu-ulisboa-specifications.
 *
 * FenixEdu fenixedu-ulisboa-specifications is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Lesser General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * FenixEdu fenixedu-ulisboa-specifications is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public License
 * along with FenixEdu fenixedu-ulisboa-specifications.  If not, see <http://www.gnu.org/licenses/>.
 */

package org.fenixedu.ulisboa.specifications.domain;


enum org.fenixedu.academic.domain.ShiftType as ShiftTypeSpecifications;
enum org.fenixedu.academic.domain.curricularRules.executors.ruleExecutors.CurricularRuleLevel as CurricularRuleLevel;
enum org.fenixedu.ulisboa.specifications.domain.serviceRequests.UIComponentType as UIComponentType;
enum org.fenixedu.ulisboa.specifications.domain.studentCurriculum.AggregationEnrolmentType as AggregationEnrolmentType;
enum org.fenixedu.ulisboa.specifications.domain.studentCurriculum.AggregationMemberEvaluationType as AggregationMemberEvaluationType;
enum org.fenixedu.ulisboa.specifications.domain.studentCurriculum.AggregationGradeCalculator as AggregationGradeCalculator;


external class .org.fenixedu.bennu.core.domain.User;
external class .org.fenixedu.bennu.io.domain.GenericFile;
external class .org.fenixedu.academic.domain.CompetenceCourse;
external class .org.fenixedu.academic.domain.DegreeInfo;
external class .org.fenixedu.academic.domain.Enrolment;
external class .org.fenixedu.academic.domain.EnrolmentEvaluation;
external class .org.fenixedu.academic.domain.Evaluation;
external class .org.fenixedu.academic.domain.EvaluationSeason;
external class .org.fenixedu.academic.domain.ExecutionInterval;
external class .org.fenixedu.academic.domain.ExecutionSemester;
external class .org.fenixedu.academic.domain.ExecutionYear;
external class .org.fenixedu.academic.domain.OccupationPeriod;
external class .org.fenixedu.academic.domain.SchoolClass;
external class .org.fenixedu.academic.domain.Shift;
external class .org.fenixedu.academic.domain.StudentCurricularPlan;
external class .org.fenixedu.academic.domain.Person;
external class .org.fenixedu.academic.domain.curricularPeriod.CurricularPeriod;
external class .org.fenixedu.academic.domain.curricularRules.AnyCurricularCourseExceptions;
external class .org.fenixedu.academic.domain.curricularRules.Rule;
external class .org.fenixedu.academic.domain.degree.DegreeType;
external class .org.fenixedu.academic.domain.degreeStructure.Context;
external class .org.fenixedu.academic.domain.serviceRequests.AcademicServiceRequest;
external class .org.fenixedu.academic.domain.serviceRequests.ServiceRequestType;
external class .org.fenixedu.academic.domain.serviceRequests.documentRequests.DocumentPurposeTypeInstance;
external class .org.fenixedu.academic.domain.student.Registration;
external class .org.fenixedu.academic.domain.student.RegistrationDataByExecutionYear;
external class .org.fenixedu.academic.domain.student.StatuteType;
external class .org.fenixedu.academic.domain.studentCurriculum.ExternalEnrolment;
external class .org.fenixedu.academic.domain.studentCurriculum.CurriculumLine;
external class .org.fenixedu.academic.domain.organizationalStructure.Unit;
external class .org.fenixedu.academic.domain.EvaluationConfiguration;
external class .org.fenixedu.academic.domain.DegreeCurricularPlan;
external class .org.fenixedu.academic.domain.degreeStructure.CourseGroup;
external class .org.fenixedu.academic.domain.Degree;
external class .org.fenixedu.academic.domain.Country;
external class .org.fenixedu.academic.domain.organizationalStructure.CountryUnit;

class UsernameSequenceGenerator {
	String prefix;
	protected Integer currentValue;
}

class idcards.CgdCard {
	String mifareCode;
	String cardNumber;
	LocalDate lastMifareModication;
	LocalDate issueDate;
	LocalDate validUntil;
	boolean temporary;
}

relation CgdCardPerson {
	idcards.CgdCard playsRole cgdCards {
		multiplicity *;
	}
	.org.fenixedu.academic.domain.Person playsRole person;
}

relation CgdCardRootDomainObject {
	idcards.CgdCard playsRole cgdCards {
		multiplicity *;
	}
	.org.fenixedu.bennu.core.domain.Bennu playsRole rootDomainObject;
}

class DisabilityType {
	String code (REQUIRED);
	LocalizedString description (REQUIRED);
	boolean other;
}

relation DisabilityTypeBennu {
	.org.fenixedu.bennu.core.domain.Bennu playsRole root {
        multiplicity 1..1;
    }
	DisabilityType playsRole disabilityTypes {
		multiplicity *;
	}
}

class UniversityDiscoveryMeansAnswer {
	String code (REQUIRED);
	LocalizedString description (REQUIRED);
	boolean other;
}

relation UniversityDiscoveryMeansAnswerBennu {
	.org.fenixedu.bennu.core.domain.Bennu playsRole root {
        multiplicity 1..1;
    }
	UniversityDiscoveryMeansAnswer playsRole universityDiscoveryMeansAnswers {
		multiplicity *;
	}
}

class UniversityChoiceMotivationAnswer {
	String code (REQUIRED);
	LocalizedString description (REQUIRED);
	boolean other;
}

relation UniversityChoiceMotivationAnswerBennu {
	.org.fenixedu.bennu.core.domain.Bennu playsRole root {
        multiplicity 1..1;
    }
	UniversityChoiceMotivationAnswer playsRole universityChoiceMotivationAnswers {
		multiplicity *;
	}
}

class SalarySpan {
	String code (REQUIRED);
	LocalizedString description (REQUIRED);
	boolean other;
}

relation SalarySpanBennu {
	.org.fenixedu.bennu.core.domain.Bennu playsRole root {
        multiplicity 1..1;
    }
	SalarySpan playsRole salarySpans {
		multiplicity *;
	}
}

class ResidenceType {
	String code (REQUIRED);
	LocalizedString description (REQUIRED);
	boolean other;
}

relation ResidenceTypeBennu {
	.org.fenixedu.bennu.core.domain.Bennu playsRole root {
        multiplicity 1..1;
    }
	ResidenceType playsRole residenceTypes {
		multiplicity *;
	}
}

class ProfessionTimeType {
	String code (REQUIRED);
	LocalizedString description (REQUIRED);
	boolean other;
}

relation ProfessionTimeTypeBennu {
	.org.fenixedu.bennu.core.domain.Bennu playsRole root {
        multiplicity 1..1;
    }
	ProfessionTimeType playsRole professionTimeTypes {
		multiplicity *;
	}
}

class PersonUlisboaSpecifications {
	LocalDate vaccinationValidity;
	boolean hasDisabilities;
	String otherDisabilityType;
	//Boolean because this slot must support null (= not filled in) 
	Boolean needsDisabilitySupport;
	String otherUniversityDiscoveryMeans;
	String otherUniversityChoiceMotivation;
	String otherDislocatedResidenceType;
    String firstOptionDegreeDesignation;
    boolean authorizeSharingDataWithCGD;
    boolean sharingDataWithCGDAnswered;
    String dgesTempIdCode;
    String oldInformation;
    boolean disabilitiesFormAnswered;
    boolean motivationsExpectationsFormAnswered;
    String personnelNumber;
    Boolean flunkedHighSchool;
    Integer flunkedHighSchoolTimes;
    Boolean flunkedPreHighSchool;
    Integer flunkedPreHighSchoolTimes;
    Boolean socialBenefitsInHighSchool;
    String socialBenefitsInHighSchoolDescription;
    Boolean firstTimeInPublicUniv;
    Integer publicUnivCandidacies;
    Boolean firstTimeInUlisboa;
    String bestQualitiesInThisCicle;
}

relation PersonUlPerson {
	PersonUlisboaSpecifications playsRole personUlisboaSpecifications;
	.org.fenixedu.academic.domain.Person playsRole person;
}

relation PersonUlSecondNationality {
	PersonUlisboaSpecifications playsRole personUlisboaSpecifications {
		multiplicity *;
	}
    .org.fenixedu.academic.domain.Country playsRole secondNationality;
}

relation PersonUlFirstOptionInstitution {
	PersonUlisboaSpecifications playsRole personUlisboaSpecifications {
		multiplicity *;
	}
    .org.fenixedu.academic.domain.organizationalStructure.Unit playsRole firstOptionInstitution;
}

relation PersonUlResidenceType {
	PersonUlisboaSpecifications playsRole personUlisboaSpecifications {
		multiplicity *;
	}
	ResidenceType playsRole dislocatedResidenceType;
}

relation PersonUlDisabilityType {
	PersonUlisboaSpecifications playsRole personUlisboaSpecifications {
		multiplicity *;
	}
	DisabilityType playsRole disabilityType;
}


relation PersonUlUniversityDiscoveryMeansAnswer {
	PersonUlisboaSpecifications playsRole personUlisboaSpecifications {
		multiplicity *;
	}
	UniversityDiscoveryMeansAnswer playsRole universityDiscoveryMeansAnswers {
		multiplicity *;
	}
}

relation PersonUlUniversityChoiceMotivationAnswer {
	PersonUlisboaSpecifications playsRole personUlisboaSpecifications {
		multiplicity *;
	}
	UniversityChoiceMotivationAnswer playsRole universityChoiceMotivationAnswers {
		multiplicity *;
	}
}

relation PrecedentDegreeInformationDistrict{
	.org.fenixedu.academic.domain.student.PrecedentDegreeInformation playsRole precedentDegreeInformation;
	.org.fenixedu.academic.domain.District playsRole district;
}

relation PrecedentDegreeInformationDistrictSubdivision{
	.org.fenixedu.academic.domain.student.PrecedentDegreeInformation playsRole precedentDegreeInformation;
	.org.fenixedu.academic.domain.DistrictSubdivision playsRole districtSubdivision;
}

class PersonUlisboaSpecificationsByExecutionYear {
    Boolean remuneratedActivityInPast;
    String remuneratedActivityInPastDescription;
    Boolean currentRemuneratedActivity;
    String currentRemuneratedActivityDescription;
    Boolean livesAlone;
    Boolean livesWithMother;
    Boolean livesWithFather;
    Boolean livesWithStepFather;
    Boolean livesWithStepMother;
    Boolean livesWithBrothers;
    Boolean livesWithChildren;
    Boolean livesWithLifemate;
    Boolean livesWithOthers;
    String livesWithOthersDesc;
    Integer numBrothers;
    Integer numChildren;
    Boolean flunkedUniversity;
    Integer flunkedUniversityTimes;
    String formsAnswered;
}

relation PersonUlExecutionYear {
    PersonUlisboaSpecificationsByExecutionYear playsRole personUlExecutionYears {
        multiplicity *;
    }
    
    .org.fenixedu.academic.domain.ExecutionYear playsRole executionYear;
}

relation PersonUlExecutionYearPersonUl {
    PersonUlisboaSpecificationsByExecutionYear playsRole personUlExecutionYears {
        multiplicity *;
    }

    PersonUlisboaSpecifications playsRole personUl;
}

relation PersonUlExecutionYearProfessionTimeType {
    PersonUlisboaSpecificationsByExecutionYear playsRole personUlExecutionYears {
        multiplicity *;
    }
    ProfessionTimeType playsRole professionTimeType;
}

relation PersonUlExecutionYearSalarySpan {
    PersonUlisboaSpecificationsByExecutionYear playsRole personUlExecutionYears {
        multiplicity *;
    }
    SalarySpan playsRole householdSalarySpan;
}

// ==================== ULisboa Specifications Root ====================

class ULisboaSpecificationsRoot {
}

relation ULisboaSpecificationsRootBennu {
	ULisboaSpecificationsRoot playsRole uLisboaSpecificationsRoot;
	.org.fenixedu.bennu.core.domain.Bennu playsRole rootDomainObject;
}

relation ULisboaSpecificationsRootRegistrationProtocol {
	ULisboaSpecificationsRoot playsRole uLisboaSpecificationsRoot;
	.org.fenixedu.academic.domain.student.RegistrationProtocol playsRole defaultRegistrationProtocol;
}

class ContingentToIngression {
	String contingent;
}

relation ContingentToIngressionIngressionType {
	.org.fenixedu.academic.domain.candidacy.IngressionType playsRole ingressionType;
	ContingentToIngression playsRole contingentToIngressions {
		multiplicity *;
	}
}

relation ULisboaSpecificationsRootContingentToIngressions {
	ULisboaSpecificationsRoot playsRole uLisboaSpecificationsRoot;
	ContingentToIngression playsRole contingentToIngressions {
		multiplicity *;
	}
}

relation ULisboaSpecificationsRootUsernameSequenceGenerator {
	UsernameSequenceGenerator playsRole usernameSequenceGenerator;
	ULisboaSpecificationsRoot playsRole uLisboaSpecificationsRoot;
}

// ==================== DGES Student Import ====================

class student.access.importation.DgesStudentImportationProcess extends .org.fenixedu.academic.domain.QueueJobWithFile {
	EntryPhase entryPhase;
}

class student.access.importation.DgesStudentImportationFile extends .org.fenixedu.bennu.io.domain.GenericFile {
}

relation ImportCandidaciesFromDgesProcessDgesImportationStudentFile {
	student.access.importation.DgesStudentImportationProcess playsRole dgesStudentImportationProcess;
	student.access.importation.DgesStudentImportationFile playsRole dgesStudentImportationFile;
}

relation DgesStudentImportationProcessExecutionYear {
	.org.fenixedu.academic.domain.ExecutionYear playsRole executionYear;
	student.access.importation.DgesStudentImportationProcess playsRole dgesStudentImportationProcess {
		multiplicity *;
	}
}

relation DgesStudentImportationProcessSpace {
	.org.fenixedu.spaces.domain.Space playsRole space;
	student.access.importation.DgesStudentImportationProcess playsRole dgesStudentImportationProcess {
		multiplicity*;
	}
}


class candidacy.FirstTimeCandidacy extends .org.fenixedu.academic.domain.candidacy.StudentCandidacy {
}


// ==================== First Year School Configuration ====================

class FirstYearRegistrationConfiguration{
	boolean requiresVaccination;
}

relation DegreeFirstYearRegistrationConfiguration{
	.org.fenixedu.academic.domain.Degree playsRole degree{
		multiplicity 1;
	}
	FirstYearRegistrationConfiguration playsRole firstYearRegistrationConfigurations {
	    multiplicity *;
	}
}

relation DegreeCurricularPlanFirstYearRegistrationConfiguration{
    .org.fenixedu.academic.domain.DegreeCurricularPlan playsRole degreeCurricularPlan {
        multiplicity 0..1;
    }
    FirstYearRegistrationConfiguration playsRole firstYearRegistrationConfigurations {
        multiplicity *;
    }
}

class FirstYearRegistrationGlobalConfiguration {
	LocalizedString introductionText;
}

class CgdMod43Template extends .org.fenixedu.bennu.io.domain.GenericFile {
}

relation BennuFirstYearRegistrationGlobalConfiguration {
	.org.fenixedu.bennu.core.domain.Bennu playsRole bennu;
	FirstYearRegistrationGlobalConfiguration playsRole firstYearRegistrationGlobalConfigurations {
		multiplicity *;
	}
}

relation DegreeConfigurationGlobalConfiguration {
	FirstYearRegistrationGlobalConfiguration playsRole globalConfiguration {
		multiplicity 1;
	}
	FirstYearRegistrationConfiguration playsRole firstYearRegistrationConfigurations {
		multiplicity *;
	}
}

relation GlobalConfigurationTemplate {
	FirstYearRegistrationGlobalConfiguration playsRole globalConfiguration {
		multiplicity 1;
	}
	CgdMod43Template playsRole mod43Template;
}	

//Class to bypass PortalLayoutInjector limitations. Check the java file for more information
class ULisboaPortalConfiguration{
	String teacherEvaluationsFunctionality;
	boolean teacherAdHocEvaluationsAvailable;
	Boolean teacherEvaluationSectionAvailable;
}

relation ULisboaPortalConfigurationPortalConfiguration{
	.org.fenixedu.bennu.portal.domain.PortalConfiguration playsRole portal{
		multiplicity 1;
	}
	ULisboaPortalConfiguration playsRole ulisboaPortal{
		multiplicity 1;
	}
}


// ==================== BEGIN: MaximumNumberOfCreditsForEnrolmentPeriodEnforcer ====================

class MaximumNumberOfCreditsForEnrolmentPeriodEnforcer {
	Double maxCredits;
	Double maxCreditsPartialTime;
}

relation ULisboaSpecificationsRootMaximumNumberOfCreditsForEnrolmentPeriodEnforcer {
	ULisboaSpecificationsRoot playsRole uLisboaSpecificationsRoot;
	MaximumNumberOfCreditsForEnrolmentPeriodEnforcer playsRole maximumNumberOfCreditsForEnrolmentPeriodEnforcer {
		multiplicity 0..1;
	}
}

// ==================== END: MaximumNumberOfCreditsForEnrolmentPeriodEnforcer ====================


// ==================== BEGIN: CurricularRule ====================

class curricularRules.CurriculumAggregatorApproval extends .org.fenixedu.academic.domain.curricularRules.Rule {
}

// ==================== END: CurricularRule ====================


// ==================== Parishes as domain objects
class Parish{
	String code (REQUIRED);
    String name (REQUIRED);
}

relation ParsishDistrictSubdivision{
	.org.fenixedu.academic.domain.DistrictSubdivision playsRole districtSubdivision{
		multiplicity 1;
	}
	
	Parish playsRole parish{
		multiplicity *;
	}
}

relation ParsishesRoot {
	Parish playsRole parishes {
		multiplicity *;
	}
	.org.fenixedu.bennu.core.domain.Bennu playsRole root;
}


// ==================== END: Parishes as domain objects ====================


// ======================= = HelpdeskConfigurations = =========================

class helpdesk.HelpdeskConfigurations {
}

class helpdesk.HelpdeskRecipient {
	String email;
}

relation BennuHelpdeskConfigurations {
	helpdesk.HelpdeskConfigurations playsRole helpdeskConfiguration;
	.org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
		multiplicity 1..1;
	}
}

relation HelpdeskRecipients {
	helpdesk.HelpdeskConfigurations playsRole recipientConfiguration;
	helpdesk.HelpdeskRecipient playsRole recipients {
		multiplicity *;
	}
}

relation HelpdeskCCs {
	helpdesk.HelpdeskConfigurations playsRole cCConfiguration;
	helpdesk.HelpdeskRecipient playsRole cCs {
		multiplicity *;
	}
}

relation HelpdeskBCCs {
	helpdesk.HelpdeskConfigurations playsRole bCCConfiguration;
	helpdesk.HelpdeskRecipient playsRole bCCs {
		multiplicity *;
	}
}

// ======================= = ====================== = =========================

// ======================= = Access control specific for 2nd cycle candidates = =========================
class SpecialIngressionsAndOtherCyclesPersistentGroup extends .org.fenixedu.bennu.core.domain.groups.GroupConstant{
}
class SecondCycleFirstYearPersistentGroup extends .org.fenixedu.bennu.core.domain.groups.GroupConstant{
}

// ======================= = Enrolment in Scool Classes begin = =========================



relation SchoolClassPrecedence {
    .org.fenixedu.academic.domain.SchoolClass playsRole nextSchoolClass;
    .org.fenixedu.academic.domain.SchoolClass playsRole previousSchoolClasses {
        multiplicity *;
    }
}


// ======================= = Enrolment in Scool Classes end = =========================


// ======================= = ULisboaServiceRequest = =========================

class serviceRequests.ULisboaServiceRequest extends .org.fenixedu.academic.domain.serviceRequests.AcademicServiceRequest {
    boolean isValid;
    boolean requestedOnline;
}

//====================== = ULisboaServiceRequestGeneratedDocument = =========================
class serviceRequests.ULisboaServiceRequestGeneratedDocument extends .org.fenixedu.bennu.io.domain.GenericFile;

relation ServiceRequestGeneratedDocumentOperator {
    .org.fenixedu.academic.domain.Person playsRole operator;
    serviceRequests.ULisboaServiceRequestGeneratedDocument playsRole generatedDocuments {
        multiplicity *;
    }
}

relation ServiceRequestGeneratedDocumentRequester {
    .org.fenixedu.academic.domain.Person playsRole requester;
    serviceRequests.ULisboaServiceRequestGeneratedDocument playsRole requestedDocuments {
        multiplicity *;
    }
}

relation ULisboaServiceRequestGeneratedDocuments {
    serviceRequests.ULisboaServiceRequest playsRole serviceRequest;
    serviceRequests.ULisboaServiceRequestGeneratedDocument playsRole generatedDocuments {
        multiplicity *;
    }
}

//======================= = ULisboaServiceRequestValidator = =========================
class serviceRequests.processors.ULisboaServiceRequestProcessor {
    LocalizedString name;
    Boolean exclusiveTransation;
}

class serviceRequests.processors.ValidateProgramConclusionProcessor extends serviceRequests.processors.ULisboaServiceRequestProcessor {
    
}

class serviceRequests.processors.StateLoggerProcessor extends serviceRequests.processors.ULisboaServiceRequestProcessor {
    
}

class serviceRequests.processors.FillStandAlonePropertyProcessor extends serviceRequests.processors.ULisboaServiceRequestProcessor {

}

class serviceRequests.processors.ValidateEnrolmentsExistenceByYearProcessor extends serviceRequests.processors.ULisboaServiceRequestProcessor {

}

class serviceRequests.processors.FillEnrolmentsByYearPropertyProcessor extends serviceRequests.processors.ULisboaServiceRequestProcessor {

}

class serviceRequests.processors.FillStandaloneEnrolmentsByYearPropertyProcessor extends serviceRequests.processors.ULisboaServiceRequestProcessor {

}

class serviceRequests.processors.FillExtracurricularEnrolmentsByYearPropertyProcessor extends serviceRequests.processors.ULisboaServiceRequestProcessor {

}

class serviceRequests.processors.FillExtracurricularApprovementsPropertyProcessor extends serviceRequests.processors.ULisboaServiceRequestProcessor {

}

class serviceRequests.processors.FillApprovedEnrolmentsPropertyProcessor extends serviceRequests.processors.ULisboaServiceRequestProcessor {

}

class serviceRequests.processors.FillAllPlansApprovementsPropertyProcessor extends serviceRequests.processors.ULisboaServiceRequestProcessor {

}

class serviceRequests.processors.FillCurriculumPropertyProcessor extends serviceRequests.processors.ULisboaServiceRequestProcessor {

}

class serviceRequests.processors.AutomaticOnlineRequestProcessor extends serviceRequests.processors.ULisboaServiceRequestProcessor {

}

class serviceRequests.processors.AutomaticRequestProcessor extends serviceRequests.processors.ULisboaServiceRequestProcessor {

}

class serviceRequests.processors.FillActiveEnrolmentsPropertyProcessor extends serviceRequests.processors.ULisboaServiceRequestProcessor {

}

class serviceRequests.processors.FillNotApprovedEnrolmentsPropertyProcessor extends serviceRequests.processors.ULisboaServiceRequestProcessor {

}

class serviceRequests.processors.ValidateRequirementsInCreationProcessor extends serviceRequests.processors.ULisboaServiceRequestProcessor {
    String serviceRequestTypesNeeded;
}


class serviceRequests.processors.ValidateImprovementEnrolmentProcessor extends serviceRequests.processors.ULisboaServiceRequestProcessor {
    String curricularEntriesPropertyName;    
}

class serviceRequests.processors.ValidateSpecialSeasonEnrolmentProcessor extends serviceRequests.processors.ULisboaServiceRequestProcessor {
    String curricularEntriesPropertyName;
}

relation ValidateImprovementEnrolmentProcessorEvaluationSeason {
    public serviceRequests.processors.ValidateImprovementEnrolmentProcessor playsRole validateImprovementEnrolmentProcessors {
        multiplicity *;
    }

    public .org.fenixedu.academic.domain.EvaluationSeason playsRole evaluationSeason {
        multiplicity 1..1;
    }
}

relation ValidateSpecialSeasonEnrolmentProcessorEvaluationSeason {
    public serviceRequests.processors.ValidateSpecialSeasonEnrolmentProcessor playsRole validateSpecialSeasonEnrolmentProcessors {
        multiplicity *;
    }

    public .org.fenixedu.academic.domain.EvaluationSeason playsRole evaluationSeason {
        multiplicity 1..1;
    }
}

relation ULisboaServiceRequestProcessorBennu {
    protected .org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
        multiplicity 1..1;
    }

    public serviceRequests.processors.ULisboaServiceRequestProcessor playsRole uLisboaServiceRequestProcessors {
        multiplicity *;
    }
}

relation ULisboaServiceRequestProcessorServiceRequestType {
    public .org.fenixedu.academic.domain.serviceRequests.ServiceRequestType playsRole serviceRequestTypes {
        multiplicity *;
    }

    public serviceRequests.processors.ULisboaServiceRequestProcessor playsRole uLisboaServiceRequestProcessors {
        multiplicity *;
    }
}


//======================= = ServiceRequestMimeType = =========================
class serviceRequests.ServiceRequestOutputType {
  String code;
  LocalizedString label;
  String extension;
}

relation ServiceRequestOutputTypeBennu {
  protected .org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
      multiplicity 1..1;
  }

  public serviceRequests.ServiceRequestOutputType playsRole serviceRequestOutputTypes {
      multiplicity *;
  }
}

relation ServiceRequestOutputTypeServiceRequestType {    
    public .org.fenixedu.academic.domain.serviceRequests.ServiceRequestType playsRole serviceRequestTypes {
        multiplicity *;
    }

    public serviceRequests.ServiceRequestOutputType playsRole serviceRequestOutputType {
        multiplicity 0..1;
    }
}



//======================= = Service Request Slot= =========================
class serviceRequests.ServiceRequestSlot {
    String code;
    UIComponentType uiComponentType;
    LocalizedString label;
    boolean changeable;
}

relation ServiceRequestSlotBennu {
    protected .org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
        multiplicity 1..1;
    }

    public serviceRequests.ServiceRequestSlot playsRole serviceRequestSlots {
        multiplicity *;
    }
}

//======================= = Service Request Slot Entry = =========================
class serviceRequests.ServiceRequestSlotEntry {
    Boolean required;
    Boolean isPrintConfiguration;
    Boolean isEditable;
    int orderNumber;
}

relation ServiceRequestSlotEntryBennu {
    protected .org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
        multiplicity 1..1;
    }

    public serviceRequests.ServiceRequestSlotEntry playsRole serviceRequestSlotEntries {
        multiplicity *;
    }
}

relation ServiceRequestSlotEntryServiceRequestSlot {
    public serviceRequests.ServiceRequestSlot playsRole serviceRequestSlot {
        multiplicity 1..1;
    }

    public serviceRequests.ServiceRequestSlotEntry playsRole serviceRequestSlotEntries {
        multiplicity *;
    }
}
    
relation ServiceRequestSlotEntryServiceRequestType {    
    public .org.fenixedu.academic.domain.serviceRequests.ServiceRequestType playsRole serviceRequestType {
        multiplicity 1..1;
    }

    public serviceRequests.ServiceRequestSlotEntry playsRole serviceRequestSlotEntries {
        multiplicity *;
    }
}

//======================= = Service Request Property = =========================
class serviceRequests.ServiceRequestProperty {
    Locale locale;
    String string;
    LocalizedString localizedString;
    Boolean booleanValue;
    CycleType cycleType;
    Integer integer;
    DateTime dateTime;
}

relation ULisboaServiceRequestRegistration {
	public serviceRequests.ULisboaServiceRequest playsRole uLisboaServiceRequests {
		multiplicity *;
	}
	public .org.fenixedu.academic.domain.student.Registration playsRole registration;
}

relation ServiceRequestPropertyBennu {
    protected .org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
        multiplicity 1..1;
    }

    public serviceRequests.ServiceRequestProperty playsRole serviceRequestProperties {
        multiplicity *;
    }
}

relation ServiceRequestSlotServiceRequestProperty {
    public serviceRequests.ServiceRequestSlot playsRole serviceRequestSlot {
        multiplicity 1..1;
    }

    public serviceRequests.ServiceRequestProperty playsRole serviceRequestProperties {
        multiplicity *;
    }
}

relation ULisboaServiceRequestServiceRequestProperty {
    public serviceRequests.ULisboaServiceRequest playsRole uLisboaServiceRequest {
        multiplicity 0..1;
    }

    public serviceRequests.ServiceRequestProperty playsRole serviceRequestProperties {
        multiplicity *;
    }
}

relation ServiceRequestSlotEntryDefaultServiceRequestProperty {
    public serviceRequests.ServiceRequestSlotEntry playsRole serviceRequestSlotEntry {
        multiplicity 0..1;
    }

    public serviceRequests.ServiceRequestProperty playsRole defaultServiceRequestProperty {
        multiplicity 0..1;
    }
}


//slots
relation DocumentPurposeTypeInstanceServiceRequestProperty {
    public .org.fenixedu.academic.domain.serviceRequests.documentRequests.DocumentPurposeTypeInstance playsRole documentPurposeTypeInstance {
        multiplicity 0..1;
    }

    public serviceRequests.ServiceRequestProperty playsRole serviceRequestProperties {
        multiplicity *;
    }
}

relation ProgramConclusionServiceRequestProperty {
    public .org.fenixedu.academic.domain.degreeStructure.ProgramConclusion playsRole programConclusion {
        multiplicity 0..1;
    }

    public serviceRequests.ServiceRequestProperty playsRole serviceRequestProperties {
        multiplicity *;
    }
}

relation ExecutionIntervalServiceRequestProperty {
    public .org.fenixedu.academic.domain.ExecutionInterval playsRole executionInterval {
        multiplicity 0..1;
    }
    
    public serviceRequests.ServiceRequestProperty playsRole serviceRequestProperties {
        multiplicity *;
    }
}

relation EvaluationSeasonServiceRequestProperty {
    public .org.fenixedu.academic.domain.EvaluationSeason playsRole evaluationSeason {
        multiplicity 0..1;
    }
    
    public serviceRequests.ServiceRequestProperty playsRole serviceRequestProperties {
        multiplicity *;
    }
}

relation StudentCurricularPlanServiceRequestProperty {
    public .org.fenixedu.academic.domain.StudentCurricularPlan playsRole studentCurricularPlan {
        multiplicity 0..1;
    }

    public serviceRequests.ServiceRequestProperty playsRole serviceRequestProperties {
        multiplicity *;
    }
}

relation EnrolmentServiceRequestProperty {
    public .org.fenixedu.academic.domain.Enrolment playsRole enrolment {
        multiplicity 0..1;
    }

    public serviceRequests.ServiceRequestProperty playsRole enrolmentServiceRequestProperties {
        multiplicity *;
    }
}

/*
 * ***************************************************
 *    ICurriculum Entries
 * ***************************************************
 * 
 * ICurriculum Entries are stored in two variables, ExternalEnrolment and CurriculumLine, 
 * in order to create specific deletion listeners.
 */
relation ExternalEnrolmentServiceRequestProperty {
    protected .org.fenixedu.academic.domain.studentCurriculum.ExternalEnrolment playsRole externalEnrolments {
        multiplicity *;
    }

    public serviceRequests.ServiceRequestProperty playsRole serviceRequestProperties {
        multiplicity *;
    }
}

relation CurriculumLineServiceRequestProperty {
    protected .org.fenixedu.academic.domain.studentCurriculum.CurriculumLine playsRole curriculumLines {
        multiplicity *;
    }

    public serviceRequests.ServiceRequestProperty playsRole serviceRequestProperties {
        multiplicity *;
    }
}
/*
 * ***************************************************
 *    End ICurriculum Entries
 * ***************************************************
 */

//======================= = END all classes related to ULisboa Service Request = =========================


// ====================== = Service Request Restriction = ========================

class serviceRequests.ServiceRequestRestriction {
}

relation ServiceRequestTypeServiceRequestRestriction {
	.org.fenixedu.academic.domain.serviceRequests.ServiceRequestType playsRole serviceRequestType;
	
	serviceRequests.ServiceRequestRestriction playsRole serviceRequestRestrictions {
		multiplicity *;
	}	
}

relation ServiceRequestRestrictionDegreeType {
	.org.fenixedu.academic.domain.degree.DegreeType playsRole degreeType;

    serviceRequests.ServiceRequestRestriction playsRole serviceRequestRestrictions {
        multiplicity *;
    }
}

relation ServiceRequestRestrictionDegree {
	.org.fenixedu.academic.domain.Degree playsRole degree;

    serviceRequests.ServiceRequestRestriction playsRole serviceRequestRestrictions {
        multiplicity *;
    }
}

relation ServiceRequestRestrictionProgramConclusion {
	.org.fenixedu.academic.domain.degreeStructure.ProgramConclusion playsRole programConclusion;

    serviceRequests.ServiceRequestRestriction playsRole serviceRequestRestrictions {
        multiplicity *;
    }
}


// ====================== = ExtendeDegreeInfo = ========================

class ExtendedDegreeInfo {
	LocalizedString scientificAreas;
	LocalizedString studyProgrammeDuration;
	LocalizedString studyRegime;
	LocalizedString studyProgrammeRequirements;
	LocalizedString higherEducationAccess;
	LocalizedString professionalStatus;
	LocalizedString supplementExtraInformation;
	LocalizedString supplementOtherSources;
}

relation BennuExtendedDegreeInfo {
	ExtendedDegreeInfo playsRole extendedDegreeInfo {
		multiplicity *;
	}
	protected .org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
        multiplicity 1..1;
    }
}

relation DegreeInfoExtendedDegreeInfo {
    .org.fenixedu.academic.domain.DegreeInfo playsRole degreeInfo;
    ExtendedDegreeInfo playsRole extendedDegreeInfo;
}

class CourseGroupDegreeInfo {
    LocalizedString degreeName;
}

relation BennuCourseGroupDegreeInfo {
    CourseGroupDegreeInfo playsRole courseGroupDegreeInfos {
        multiplicity *;
    }
    protected .org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
        multiplicity 1..1;
    }
}

relation CourseGroupDegreeInfoExtendedDegreeInfo {
    CourseGroupDegreeInfo playsRole courseGroupDegreeInfos {
        multiplicity *;
    }
    ExtendedDegreeInfo playsRole extendedDegreeInfo {
        multiplicity 0..1;
    }
}

relation CourseGroupDegreeInfoCourseGroup {
    CourseGroupDegreeInfo playsRole courseGroupDegreeInfos {
        multiplicity *;
    }
    .org.fenixedu.academic.domain.degreeStructure.CourseGroup playsRole courseGroup {
        multiplicity 0..1;
    }
}

// ==================== BEGIN: Curriculum Aggregator ====================

    class studentCurriculum.CurriculumAggregator {
    	LocalizedString description;
    	AggregationEnrolmentType enrolmentType;
    	AggregationMemberEvaluationType evaluationType;
    	AggregationGradeCalculator gradeCalculator;
    	int gradeValueScale;
    	int optionalConcluded;
    }
    
	    relation CurriculumAggregatorYear {
		    studentCurriculum.CurriculumAggregator playsRole curriculumAggregator {
		        multiplicity 0..*;
		    }
		
		    .org.fenixedu.academic.domain.ExecutionYear playsRole since {
		        multiplicity 1;
		    }
		}

	    relation CurriculumAggregatorContext {
		    studentCurriculum.CurriculumAggregator playsRole curriculumAggregator {
		        multiplicity 0..*;
		    }
		
		    .org.fenixedu.academic.domain.degreeStructure.Context playsRole context {
		        multiplicity 1;
		    }
		}

	    relation CurriculumAggregatorEvaluationSeason {
		    protected studentCurriculum.CurriculumAggregator playsRole curriculumAggregator {
		        multiplicity 0..*;
		    }
		
		    .org.fenixedu.academic.domain.EvaluationSeason playsRole evaluationSeason {
		        multiplicity 0..1;
		    }
		}

		relation CurriculumAggregatorRoot {
			ULisboaSpecificationsRoot playsRole root {
				multiplicity 1..1;
			}
			studentCurriculum.CurriculumAggregator playsRole curriculumAggregator {
				multiplicity 0..*;
			}
		}

    class studentCurriculum.CurriculumAggregatorEntry {
    	BigDecimal gradeFactor;
    	int gradeValueScale;
    	boolean optional;
    	boolean supportsTeacherConfirmation;
    }
    
	    relation CurriculumAggregatorEntryContext {
		    studentCurriculum.CurriculumAggregatorEntry playsRole curriculumAggregatorEntry {
		        multiplicity 0..*;
		    }
		
		    .org.fenixedu.academic.domain.degreeStructure.Context playsRole context {
		        multiplicity 1;
		    }
		}

	    relation CurriculumAggregatorEntries {
		    studentCurriculum.CurriculumAggregator playsRole aggregator {
		        multiplicity 1;
		    }
		
		    studentCurriculum.CurriculumAggregatorEntry playsRole entries {
		        multiplicity 0..*;
		    }
		}

// ==================== END: Curriculum Aggregator ====================


// ======================= Registration Observations =========================
class RegistrationObservations{
	String value;
}

relation RegistrationObservationRegistration{
	protected .org.fenixedu.academic.domain.student.Registration playsRole registration {
        multiplicity 1..1;
    }
    
    RegistrationObservations playsRole registrationObservations{
    	multiplicity *;
    }
}
// ======================= Registration Observations end =========================


/*
* Temporary Files
*/
class file.ULisboaSpecificationsTemporaryFile extends .org.fenixedu.bennu.io.domain.GenericFile {
        
}

relation ULisboaSpecificationsTemporaryFileRoot {
   
    ULisboaSpecificationsRoot playsRole uLisboaSpecificationsRoot {
    	multiplicity 1..1;
    }
    
    file.ULisboaSpecificationsTemporaryFile playsRole temporaryFiles {
        multiplicity *;
    }
}

relation ULisboaSpecificationsTemporaryFileUser {

	.org.fenixedu.bennu.core.domain.User playsRole uLisboaTemporaryFileOwner {
		multiplicity 1..1;
	}

	file.ULisboaSpecificationsTemporaryFile playsRole uLisboaTemporaryFiles {
        multiplicity *;
    }
}


/* Precedent Degrees */

relation DegreePrecedentDegree {
	.org.fenixedu.academic.domain.Degree playsRole precedentDegrees {
		multiplicity *;
	}
	
	.org.fenixedu.academic.domain.Degree playsRole subsequentDegrees {
		multiplicity *;
	}
}

/*
 * The country where the student finished the high school should be associated with
 * Person and not PrecedentDegreeInformation. It is invariant information across
 * Registrations
 */ 
relation PersonCountryHighSchool {
    .org.fenixedu.academic.domain.Person playsRole {
    	multiplicity *;
    }
    
    .org.fenixedu.academic.domain.Country playsRole countryHighSchool;
}

class MobilityRegistatrionUlisboaInformation {
    SchoolPeriodDuration programDuration;
    String otherOriginMobilityProgrammeLevel;
    String otherIncomingMobilityProgrammeLevel;
    LocalDate beginDate;
    LocalDate endDate;
}

relation MobilityRegistatrionUlisboaInformationBennu {
    .org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
        multiplicity 1..1;
    }

    MobilityRegistatrionUlisboaInformation playsRole mobilityRegistatrionUlisboaInformations {
        multiplicity *;
    }
}

relation MobilityRegistatrionUlisboaInformationMobilityProgramType {
    MobilityRegistatrionUlisboaInformation playsRole mobilityRegistatrionUlisboaInformations {
        multiplicity *;
    }

    .org.fenixedu.academic.domain.student.mobility.MobilityProgramType playsRole mobilityProgramType;
}

relation MobilityRegistatrionUlisboaInformationMobilityActivityType {
    MobilityRegistatrionUlisboaInformation playsRole mobilityRegistatrionUlisboaInformations {
        multiplicity *;
    }

    .org.fenixedu.academic.domain.student.mobility.MobilityActivityType playsRole mobilityActivityType;
}

relation MobilityRegistatrionUlisboaInformationRegistration {

    MobilityRegistatrionUlisboaInformation playsRole mobilityRegistatrionUlisboaInformation {
        multiplicity 0..1;
    }

    PersonUlisboaSpecifications playsRole personUlisboaSpecification {
        multiplicity 1..1;
    }
}

relation MobilityRegistatrionUlisboaInformationBeginExecutionSemester {
    
    MobilityRegistatrionUlisboaInformation playsRole mobilityRegistatrionUlisboaInformationsForBegin {
        multiplicity *;
    }
    
    .org.fenixedu.academic.domain.ExecutionSemester playsRole begin;
}

relation MobilityRegistatrionUlisboaInformationEndExecutionSemester {
    
    MobilityRegistatrionUlisboaInformation playsRole mobilityRegistatrionUlisboaInformationsForEnd {
        multiplicity *;
    }

    .org.fenixedu.academic.domain.ExecutionSemester playsRole end;
}
   
relation MobilityRegistatrionUlisboaInformationMobilityScientificArea {
    MobilityRegistatrionUlisboaInformation playsRole mobilityRegistatrionUlisboaInformations {
        multiplicity *;
    }

    .org.fenixedu.academic.domain.student.mobility.MobilityScientificArea playsRole mobilityScientificArea {
        multiplicity 0..1;
    }
}

relation MobilityRegistatrionUlisboaInformationMobilityProgrammeLevel {
    MobilityRegistatrionUlisboaInformation playsRole mobilityRegistatrionUlisboaInformationsForIncoming {
        multiplicity *;
    }

    .org.fenixedu.academic.domain.student.mobility.MobilityProgrammeLevel playsRole incomingMobilityProgrammeLevel {
        multiplicity 0..1;
    }
}

relation MobilityRegistatrionUlisboaInformationOriginMobilityProgrammeLevel {
    MobilityRegistatrionUlisboaInformation playsRole mobilityRegistatrionUlisboaInformationsForOrigin {
        multiplicity *;
    }

    .org.fenixedu.academic.domain.student.mobility.MobilityProgrammeLevel playsRole originMobilityProgrammeLevel {
        multiplicity 0..1;
    }
}

relation MobilityRegistatrionUlisboaInformationOriginCountry {
    MobilityRegistatrionUlisboaInformation playsRole mobilityRegistatrionUlisboaInformationsForOrigin {
        multiplicity *;
    }

    .org.fenixedu.academic.domain.Country playsRole originCountry {
        multiplicity 0..1;
    }
}

relation MobilityRegistatrionUlisboaInformationIncomingCountry {
    MobilityRegistatrionUlisboaInformation playsRole mobilityRegistatrionUlisboaInformationsForIncoming {
        multiplicity *;
    }

    .org.fenixedu.academic.domain.Country playsRole incomingCountry {
        multiplicity 0..1;
    }
}

// ========================= Student active override ==============================

class student.ActiveStudentOverride {
	IDDocumentType idDocumentType;
	String idDocumentNumber;
	String oldStudentNumber;
}

relation ActiveStudentOverrideBennu {
    public student.ActiveStudentOverride playsRole activeStudentOverrides {
        multiplicity *;
    }

    protected .org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
        multiplicity 1..1;
    }
}
// ========================= End Student active override ==============================

class tuitionpenalty.TuitionPenaltyConfiguration {
}

	relation TuitionPenaltyConfigurationBennu {
	    .org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
	        multiplicity 1..1;
	    }
		
		tuitionpenalty.TuitionPenaltyConfiguration playsRole tuitionPenaltyConfigurations {
			multiplicity *;
		}
	}

	relation TuitionPenaltyConfigurationServiceRequestType {
		tuitionpenalty.TuitionPenaltyConfiguration playsRole tuitionPenaltyConfiguration {
			multiplicity 0..1;
		}
	
		.org.fenixedu.academic.domain.serviceRequests.ServiceRequestType playsRole tuitionPenaltyServiceRequestType {
			multiplicity 0..1;
		}
	}

	relation TuitionPenaltyConfigurationServiceRequestSlot {
		tuitionpenalty.TuitionPenaltyConfiguration playsRole tuitionPenaltyConfigurationForTuitionInstallmentOrder {
			multiplicity 0..1;
		}

		serviceRequests.ServiceRequestSlot playsRole tuitionInstallmentOrderSlot {
			multiplicity 0..1;
		}
	}
	
	relation TuitionPenaltyConfigurationExecutionYearServiceRequestSlot {
		tuitionpenalty.TuitionPenaltyConfiguration playsRole tuitionPenaltyConfigurationForExecutionYear {
			multiplicity 0..1;
		}

		serviceRequests.ServiceRequestSlot playsRole executionYearSlot {
			multiplicity 0..1;
		}
	}

	


class bluerecord.BlueRecordConfiguration {
    Boolean isCgdFormToFill;
}


relation BlueRecordConfigurationBennu {
    bluerecord.BlueRecordConfiguration playsRole blueRecordConfiguration {
        multiplicity 0..1;
    }
    
    .org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
        multiplicity 1..1;
    }
}

relation ExclusiveDegreesBlueRecordConfiguration{
    .org.fenixedu.academic.domain.Degree playsRole exclusiveDegrees {
        multiplicity *;
    }
    bluerecord.BlueRecordConfiguration playsRole blueRecordConfiguration {
        multiplicity 0..1;
    }
}

